BASE <http://cat.nationalarchives.gov.uk/>

PREFIX cat: <http://cat.nationalarchives.gov.uk/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX nat: <http://www.nationalarchives.gov.uk/>
PREFIX odrl: <http://www.w3.org/ns/odrl/2/>

CONSTRUCT {
          ?policyUri a odrl:Policy, odrl:Offer ;
                     odrl:profile nat:odrl-profile ;
                     odrl:conflict ?conflict ;
                     odrl:permission ?b1 ;
                     odrl:prohibition ?b2 .
          ?b1 ?p1 ?v1 ;
              odrl:assigner cat:The_National_Archives ;
              odrl:target ?resource .
          ?b2 ?p2 ?v2 ;
              odrl:assigner cat:The_National_Archives ;
              odrl:target ?resource .
          ?b1 odrl:constraint ?c1 .
          ?c1 ?c2 ?c3 .
          }
WHERE
{
    BIND(cat:ADM.2021.21L1TH.P.1 AS ?resource)
    ?resource dct:identifier ?identifier
    BIND(URI(CONCAT(?identifier,"-policy")) AS ?policyUri)
    {
        {
            SELECT DISTINCT ?permission
            WHERE {
                      BIND(cat:ADM.2021.21L1TH.P.1 AS ?resource)
                      ?resource dct:accessRights ?accessRights .
                      ?accessRights odrl:hasPolicy ?policy .
                      { ?policy odrl:permission ?permission . }
                      UNION
                      {
                          ?policy odrl:inheritFrom+ ?parentPolicy .
                          ?parentPolicy odrl:permission ?permission
                      }
                  }
            }
        BIND(BNODE() AS ?b1)
        OPTIONAL {
                     ?permission odrl:constraint ?constraint .
                     BIND(BNODE() AS ?c1)
                     ?constraint ?c2 ?c3 .
                 }
        ?permission ?p1 ?v1 .
        FILTER(?p1 NOT IN(odrl:constraint)) .
    }
    UNION
    {
        {
            SELECT DISTINCT ?prohibition
            WHERE {
                      BIND(cat:ADM.2021.21L1TH.P.1 AS ?resource)
                      ?resource dct:accessRights ?accessRights .
                      ?accessRights odrl:hasPolicy ?policy .
                      { ?policy odrl:prohibition ?prohibition . }
                      UNION
                      {
                          ?policy odrl:inheritFrom+ ?parentPolicy .
                          ?parentPolicy odrl:prohibition ?prohibition
                      }
                  }
            }
        BIND(BNODE() AS ?b2)
        ?prohibition ?p2 ?v2 .
    }
    {
        SELECT DISTINCT ?conflict
        WHERE {
                  BIND(cat:ADM.2021.21L1TH.P.1 AS ?resource)
                  ?resource dct:accessRights ?accessRights .
                  ?accessRights odrl:hasPolicy ?policy .
                  { ?policy odrl:conflict ?conflict . }
                  UNION
                  {
                      ?policy odrl:inheritFrom+ ?parentPolicy .
                      ?parentPolicy odrl:conflict ?conflict .
                  }
              }
        }
}
